/*
 * build.gradle
 * CLGen
 *
 * Copyright Â© 2022 Mill Dam Consulting Ltd. All rights reserved.
 */

plugins {
    id 'antlr'
    id 'application'
    id 'groovy'
}

repositories {
    mavenCentral()
}

project.version = '1.3.0'

application {
    mainClass = 'org.flightgear.clgen.CLGen'
}

dependencies {
    antlr 'org.antlr:antlr4:4.9.3'
    compileOnly 'org.antlr:antlr4-runtime:4.9.3'
    implementation(
        'org.apache.velocity:velocity:1.7',
        'com.github.librepdf:openpdf:1.3.26'
    )
    testImplementation(
        'org.codehaus.groovy:groovy-all:3.0.8',
        'cglib:cglib-nodep:3.3.0',
        'org.spockframework:spock-core:2.0-groovy-3.0'
    )
}


generateGrammarSource {
    arguments += ["-package", "org.flightgear.clgen"]
}

processResources {
    filesMatching('clgen.properties') {
        expand projectVersion: project.version
    }
}

jar {
    manifest {
        attributes('Main-Class': 'org.flightgear.clgen.CLGen')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
    exclude "META-INF/*.txt"
}

task release(type: Zip, dependsOn: ['build', 'vim']) {
    println "Don't forget to update version number in both scripts"
    destinationDirectory = file('build/release')
    into("CLGen-${project.version}") {
        from file('clgen')
        from file('clgen.bat')
        from file('README.md')
    }
    into("CLGen-${project.version}/build/libs") {
        from fileTree('build/libs')
        include("**/CLGen-${project.version}.jar")
    }
    into("CLGen-${project.version}/samples") {
        from fileTree('samples')
        include('**/*.clg')
    }
}

task vim(type: Zip) {
    destinationDirectory = file('build/release')
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    archiveAppendix = "Vim"
    into("start") {
        from fileTree('support/vim')
        include('**/*.vim')
    }
}

release.doLast { zip ->
    ant.checksum file: zip.archivePath
}
